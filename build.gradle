buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven { url 'https://maven.minecraftforge.net/' }
        maven { url = "https://repo.spongepowered.org/maven" }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id 'java'
    id 'maven-publish'
    id 'net.covers1624.signing' version '1.1.4'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: "org.spongepowered.mixin"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

sourceSets.main.resources.srcDirs += "src/main/generated"

def signProps = [:]
if (System.getenv('KEY_STORE')) {
    println 'Using Env variables for jar signing.'
    signProps['keyStore'] = System.getenv('KEY_STORE')
    file(System.getenv('KEY_STORE_PROPS')).withReader {
        def props = new Properties()
        props.load(it)
        signProps.putAll(props)
    }
} else if (project.hasProperty('keyStore')) {
    println 'Using Project properties for jar signing.'
    signProps['keyStore'] = project.getProperty('keyStore')
    signProps['storePass'] = project.getProperty('keyStorePass')
    signProps['alias'] = project.getProperty('keyStoreAlias')
    signProps['keyPass'] = project.getProperty('keyStoreKeyPass')
} else {
    println 'No signing secrets found, build will not be signed.'
}

mod_version = "${mod_version}." + (System.getenv("BUILD_NUMBER") ?: "1")

version = "${mc_version}-${mod_version}"
group = 'com.teamcofh'
archivesBaseName = "${mod_id}"

repositories {
    mavenLocal()
    maven {
        url 'https://maven.covers1624.net/'
    }
    maven { // TOP
        url 'https://maven.tterrag.com/'
    }
    maven { // JEI
        url 'https://dvs1.progwml6.com/files/maven/'
    }
    maven { // Patchouli
        url 'https://maven.blamejared.com/'
    }
    maven { // Curios
        url 'https://maven.theillusivec4.top/'
    }
}

minecraft {
    mappings channel: 'official', version: "${mc_version}"
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        data {
            workingDirectory project.file('run')
            ideaModule "${rootProject.name}.${project.name}.main"

            args '--all', '--output', file('src/main/generated/'),
                    '--mod', archivesBaseName,
                    '--existing', sourceSets.main.resources.srcDirs[0]

            mods {
                CoFHCore {
                    source sourceSets.main
                }
            }
        }
    }
}

mixin {
    add(sourceSets.main, "mixins.cofhcore.refmap.json")
}

configurations {
    annotationProcessor.extendsFrom minecraft
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"

    compileOnly fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}:api")
    runtimeOnly fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}")

    runtimeOnly fg.deobf("top.theillusivec4.curios:curios-forge:${curios_version}")
    compileOnly fg.deobf("top.theillusivec4.curios:curios-forge:${curios_version}:api")
}

signing {
    if (!signProps.isEmpty()) {
        jars {
            sign jar
            after 'reobfJar'

            keyStore = signProps.keyStore
            alias = signProps.alias
            storePass = signProps.storePass
            keyPass = signProps.keyPass
        }
    }
}

jar {
    manifest {
        attributes([
                'Specification-Title'     : "${mod_name}",
                'Specification-Vendor'    : 'Team CoFH',
                'Specification-Version'   : "${mc_version}",
                'Implementation-Title'    : "${mod_id}",
                'Implementation-Vendor'   : 'Team CoFH',
                'Implementation-Version'  : "${mod_version}",
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'MixinConfigs'            : 'mixins.cofhcore.json',
        ])
    }
}

task srcJar(type: Jar) {
    build.dependsOn it
    from sourceSets.main.allSource
    classifier = 'sources'
}

publishing {
    repositories {
        if (System.getenv('MAVEN_PASS')) {
            maven {
                url 'https://nexus.covers1624.net/repository/cofh-releases/'
                credentials {
                    username 'kinglemming'
                    password System.getenv('MAVEN_PASS')
                }
            }
        }
    }
    publications {
        mod(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            artifact jar
            artifact srcJar

            pom {
                name = archivesBaseName
                description = archivesBaseName
                url = "https://github.com/CoFH/${project.name}"
                scm {
                    url = "https://github.com/CoFH/${project.name}"
                    connection = "scm:git:git://github.com/CoFH/${project.name}.git"
                    connection = "scm:git:git@github.com:CoFH/${project.name}.git"
                }

                issueManagement {
                    system = 'discord'
                    url = 'https://discord.gg/uRKrnbH'
                }
                licenses {
                    license {
                        name = 'Don\'t Be a Jerk v2'
                        url = "https://github.com/CoFH/${project.name}/blob/main/README.md"
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'teamcofh'
                        name = 'Team CoFH'
                    }
                }
            }
        }
    }
}
